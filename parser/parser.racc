class Parser

token IDENT HEX_NUMBER DECI_NUMBER BIN_NUMBER EOL REG_X REG_Y STRING

rule
	program:
		| top_pseudo_operators routines
		{
			val[1].unshift Tree::RoutineNode.new(Tree::IdentiferNode.new('__top__'+randIdent(10)+'__'), val[0]);
			result = val[1];
		}
		| routines
		{
			result = val[0];
		}
		;
	top_pseudo_operators:
		pseudo_operator top_pseudo_operators
		{
			val[1].unshift val[0];
			result = val[1];
		}
		| pseudo_operator
		{
			result = [val[0]];
		}
		;

	routines:
		routine routines
		{
			val[1].unshift val[0];
			result = val[1];
		}
		| routine
		{
			result = [val[0]];
		}
		;

	routine:
		':' identifer EOL operators
		{
			result = Tree::RoutineNode.new(val[1], val[3]);
		}
		| ':' identifer EOL
		{
			result = Tree::RoutineNode.new(val[1], []);
		}
		;
	operators:
		operator operators
		{
			val[1].unshift val[0];
			result = val[1];
		}
		| pseudo_operator operators
		{
			val[1].unshift val[0];
			result = val[1];
		}
		| operator
		{
			result = [val[0]];
		}
		| pseudo_operator
		{
			result = [val[0]];
		}
		;
	pseudo_operator:
		'.' identifer exprs EOL
		{
			result = Tree::PseudoOperator::create(val[1], val[2]);
		}
		| '.' identifer EOL
		{
			result = Tree::PseudoOperator::create(val[1]);
		}
		;
	exprs:
		expr ',' exprs
		{
			val[2].appendPre(val[0]);
			result = val[2];
		}
		| expr
		{
			result = Tree::ExprListNode.new(val[0]);
		}
		;
	expr:
		immediate
		| identifer
		| string
		;
	operator:
		opcode EOL
		{
			result = Tree::Opcode.create val[0], Tree::Operand::NilOperand.new()
		}
		| opcode operand EOL
		{
			result = Tree::Opcode.create val[0], val[1]
		}
		;
	opcode: identifer;
	operand: 
		operand_arg
		{
			result = Tree::Operand::ImmediateOperand.new(val[0]);
		}
		| '[' operand_arg ']'
		{
			result = Tree::Operand::ZeropageOperand.new(val[1]);
		}
		| '[' operand_arg ',' reg ']'
		{
			case val[3]
			when :REG_X
			result = Tree::Operand::ZeropageOperand.new(val[1], Tree::Operand::X);
			when :REG_Y
			result = Tree::Operand::ZeropageOperand.new(val[1], Tree::Operand::Y);
			end
		}
		| '<' operand_arg '>'
		{
			result = Tree::Operand::AbsoluteOperand.new(val[1]);
		}
		| '<' operand_arg ',' reg '>'
		{
			case val[3]
			when :REG_X
			result = Tree::Operand::AbsoluteOperand.new(val[1], Tree::Operand::X);
			when :REG_Y
			result = Tree::Operand::AbsoluteOperand.new(val[1], Tree::Operand::Y);
			end
		}
		| '(' operand_arg ')'
		{
			result = Tree::Operand::IndirectOperand.new(val[1]);
		}
		| '(' operand_arg ',' REG_X ')'
		{
			result = Tree::Operand::IndirectOperand.new(val[1], Tree::Operand::X);
		}
		| '(' operand_arg ')' ',' REG_Y
		{
			result = Tree::Operand::IndirectOperand.new(val[1], Tree::Operand::Y);
		}
		;
	reg: REG_X | REG_Y;
	operand_arg:
		immediate
		| identifer
		;
	identifer:
		IDENT
		{
			result = Tree::IdentiferNode.new(val[0]);
		}
		;
	immediate:
		HEX_NUMBER
		{
			result = Tree::ImmediateNode.new(val[0], "0x#{val[0]}", 16);
		}
		| DECI_NUMBER
		{
			result = Tree::ImmediateNode.new(val[0], val[0], 10);
		}
		| BIN_NUMBER
		{
			result = Tree::ImmediateNode.new(val[0], "0b#{val[0]}", 2);
		}
		;
	string:
		STRING
		{
			result = Tree::StringNode.new(val[0]);
		}
		;
end
---- header
# -*- encoding: utf-8 -*-
require File.dirname(__FILE__)+"/parser_inner.rb"
---- inner
include ParserInner

